plugins {
    alias libs.plugins.hilt
    alias libs.plugins.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.serialization
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.kotlin.ksp
    alias libs.plugins.navigation.args
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.example.weatherforecast"
        minSdk 30
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "API_KEY", "\"${localProperties.getProperty('WEATHER_API_KEY')}\"")
        buildConfigField("String", "API_BASE_URL", "\"${localProperties.getProperty('WEATHER_API_BASE_URL')}\"")
    }
    buildFeatures {
        dataBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.kotlinComposeCompiler.get()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xjvm-default=all",    // To remove a build error "Inheritance from an interface with '@JvmDefault' members is only allowed with -Xjvm-default option"
        ]
    }
    namespace 'com.example.weatherforecast'
}

kapt {
    correctErrorTypes true
    javacOptions {
        // These options are normally set automatically via the Hilt Gradle plugin, but we
        // set them manually to workaround a bug in the Kotlin 1.5.20
        option("-Adagger.fastInit=ENABLED")
        option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
    }
}


dependencies {

    implementation libs.androidx.activity.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.material.icons.extended
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.core.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.hilt.common
    implementation libs.androidx.hilt.work
    implementation libs.androidx.legacy.support.v4
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.process
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.androidx.room.ktx
    implementation libs.androidx.work.runtime.ktx
    implementation libs.hilt.android
    implementation libs.hilt.android.gradle
    implementation libs.kotlin.immutableCollections
    implementation libs.kotlin.stdlib.jdk8
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.serialization.json
    implementation libs.material
    implementation libs.okhttp
    implementation libs.okhttp.logging.interceptor
    implementation libs.play.services.location
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson

    ksp libs.androidx.hilt.compiler
    ksp libs.hilt.compiler
    ksp libs.room

    testImplementation libs.junit

    androidTestImplementation libs.androidx.compose.ui.test.junit4
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.junit
    debugImplementation libs.androidx.compose.ui.tooling
}